// Drools rules for Trade validation
package com.pms.validation.rules;

import com.pms.validation.dto.TradeDto;
import com.pms.validation.dto.ValidationResultDto;
import java.math.BigDecimal;
import java.time.LocalDateTime;
import java.util.List;

global List validPortfolios;
global List validSymbols;

function void addError(ValidationResultDto res, String msg) {
    if (res != null) {
        res.addError(msg);
    }
}

function boolean isBlank(String s) {
    return s == null || s.trim().length() == 0;
}

rule "Trade - required fields"
    when
        $t : TradeDto()
        $r : ValidationResultDto()
    then
        if ($t.getTradeId() == null) addError($r, "tradeId is required");
        if ($t.getPortfolioId() == null) addError($r, "portfolioId is required");
        if (isBlank($t.getSymbol())) addError($r, "symbol is required");

        if ($t.getSide() == null) addError($r, "side is required");
        if ($t.getPricePerStock() == null) addError($r, "pricePerStock is required");
        if ($t.getQuantity() == null) addError($r, "quantity is required");
        if ($t.getTimestamp() == null) addError($r, "timestamp is required");
end


rule "Trade - portfolio must exist in DB"
when
    $t : TradeDto( portfolioId != null )
    $r : ValidationResultDto()
then
    if (!validPortfolios.contains($t.getPortfolioId())) {
        addError($r, "Invalid portfolio: " + $t.getPortfolioId());
    }
end


rule "Trade - symbol must exist in DB"
when
    $t : TradeDto( symbol != null )
    $r : ValidationResultDto()
then
    if (!validSymbols.contains($t.getSymbol())) {
        addError($r, "Invalid symbol: " + $t.getSymbol());
    }
end


rule "Trade - price must be positive"
    when
        $t : TradeDto( pricePerStock != null )
        $r : ValidationResultDto()
    then
        if ($t.getPricePerStock().compareTo(BigDecimal.ZERO) <= 0) {
            addError($r, "pricePerStock must be greater than 0");
        }
end

rule "Trade - quantity must be positive"
    when
        $t : TradeDto( quantity != null )
        $r : ValidationResultDto()
    then
        if ($t.getQuantity() <= 0) {
            addError($r, "quantity must be greater than 0");
        }
        if ($t.getQuantity() > 1000000L) {
            addError($r, "quantity exceeds maximum allowed per trade (1,000,000)");
        }
end

rule "Trade - timestamp sanity"
    when
        $t : TradeDto( timestamp != null )
        $r : ValidationResultDto()
    then
        LocalDateTime now = LocalDateTime.now();
        if ($t.getTimestamp().isAfter(now)) {
            addError($r, "timestamp cannot be in the future");
        }
        if ($t.getTimestamp().isBefore(now.minusYears(1))) {
            addError($r, "timestamp is too old (older than 1 year)");
        }
end

rule "Trade - side validation"
    when
        $t : TradeDto( side != null )
        $r : ValidationResultDto()
    then
        String sideLower = $t.getSide().toLowerCase();
        if (!sideLower.equals("buy") && !sideLower.equals("sell")) {
            addError($r, "side must be BUY or SELL");
        }
end

rule "Trade - symbol basic format"
    when
        $t : TradeDto( symbol != null )
        $r : ValidationResultDto()
    then
        String s = $t.getSymbol();
        if (s.length() < 1 || s.length() > 10) {
            addError($r, "symbol length must be between 1 and 10 characters");
        }
end

rule "Trade - high price flag"
    when
        $t : TradeDto( pricePerStock != null )
        $r : ValidationResultDto()
    then
        if ($t.getPricePerStock().compareTo(new BigDecimal("1000000")) >= 0) {
            addError($r, "pricePerStock unusually high - requires review");
        }
end
